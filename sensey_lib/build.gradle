apply plugin: 'com.android.library'
apply plugin: 'maven'

def libraryVersionCode = 2
def libraryVersion = '1.0.0'
def libraryArtifactId = 'sensey'
def libraryGroupID = POM_GROUP_ID_GITHUB


android {
    signingConfigs {
        release {
            try {
                keyAlias exKEYSTORE_ALIAS
                keyPassword exKEY_PASSWORD
                storeFile file(exKEYSTORE_FILEPATH)
                storePassword exKEYSTORE_PASSWORD
            }
            catch (ex) {
                throw new InvalidUserDataException("You should define exKEYSTORE_ALIAS, exKEYSTORE_FILEPATH, exKEYSTORE_PASSWORD and exKEY_PASSWORD in gradle.properties.")
            }
        }
    }
    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 23
        versionCode libraryVersionCode
        versionName libraryVersion
    }
    buildTypes {
        debug {
            minifyEnabled false
            debuggable true
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}


dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
}

def localMavenRepo = 'file://' + new File(System.getProperty('user.home'), LOCAL_GITHUB_MAVEN_REPO_PATH).absolutePath
uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: localMavenRepo)

            pom.version = libraryVersion
            pom.artifactId = libraryArtifactId
            pom.groupId = libraryGroupID
        }
    }
}

// Place this in the 'build.gradle' of the library module
// Task : Generate a JAR
// Usage : In the terminal, run './gradlew jar'
// Output : Get the jar at 'build/libs'

task jar(type: Jar, dependsOn: 'assembleRelease') {
    from fileTree(dir: 'build/intermediates/classes/release')
}